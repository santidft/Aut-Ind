<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="robot">
    <Locals>
      <Local name="pAprox" type="pointRx" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  // Controla los movimientos del brazo
  //
  // Variables locales
  // - pAprox: punto de aproximacion

  // Se desplaza a la posicion de espera
  pEspera=appro(pEntrada,trAprox)
  movej(pEspera,tEyector,mNomSpeed)
  waitEndMove()

  // Bucle principal de la tarea
  while !bTermina
    if bTraslada
      for nPendientes=0 to nSolicitadas-1
        // Se espera la orden de pieza preparada para continuar
        sMensaje="pulse 'enter' para continuar"
        wait(bPiezaPreparada)
        bPiezaPreparada=false
        sMensaje=""

        // Se aproxima a la posicion de entrada (origen)
        pAprox=appro(pEntrada,trAprox)
        movej(pAprox,tEyector,mNomSpeed)
        // Se coje la pieza
        movel(pEntrada,tEyector,mVelAprox)
        close(tEyector)
        movel(pAprox,tEyector,mVelAprox)

        // Se aproxima a la posicion de salida (destino)
        pAprox=appro(pSalida,trAprox)
        movej(pAprox,tEyector,mNomSpeed)
        // Se suelta la pieza
        movel(pSalida,tEyector,mVelAprox)
        open(tEyector)
        movel(pAprox,tEyector,mVelAprox)

        // Se regresa a la posicion de espera
        movej(pEspera,tEyector,mNomSpeed)
        waitEndMove()
      endFor
      bTraslada=false
    endIf

    // Se da paso a la siguiente tarea
    delay(0)
  endWhile
  bTermina=false

  // Se regresa a la posicion de referencia
  movej(jHome,tEyector,mNomSpeed)
  waitEndMove()

  // Se informa de la finalizacion de la tarea
  bFin=true
end]]></Code>
  </Program>
</Programs>